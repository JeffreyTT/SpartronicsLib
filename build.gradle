plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
    id "maven-publish"
    id "c"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Maven central needed for JUnit
repositories {
    mavenCentral()
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    compile("gov.nist.math:jama:1.0.3")

    testCompile("org.junit.jupiter:junit-jupiter-api:5.3.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.2")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "com.spartronics4915"
            artifactId = "SpartronicsLib"
            version = "2019.1.0"

            from components.java
        }
    }
}

test {
    useJUnitPlatform {
        // Some tests only work if you have the right stuff plugged in
        // Pass -PallTests if you want tests with the excluded tags
        if (!project.hasProperty("allTests")) {
            excludeTags "hardwareDependant"
        }
    }
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
}